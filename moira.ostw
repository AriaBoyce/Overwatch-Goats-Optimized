import "base.ostw";

playervar define moira_resource;
playervar define moira_heal_drain;
playervar define moira_heal_passive;
playervar define moira_heal_damage;
playervar define moira_self_heal;
//ult move speed 40->50%

//orb cooldown 8->10s
//heal orb heal 250->300

//HPS 70->80
//energy drain 12.5->11
//energy gain 3.6->2
//self heal 20->30
//dps 60->50

//fade
//remove super jump
//cooldown 7->6
//cant be used during ult

void moira_setup()
{
    base_health = 200;
    moira_resource = 100;
    moira_heal_drain = (11/156)*100;
    moira_heal_passive = (3.744/156)*100;
    moira_heal_damage = (12.14/156)*100;
    current_hero = Hero.Moira;
    tick_rate = (1/62.5)*20;

    AddHealthPoolToPlayer(EventPlayer(),HealthType.Health,175,true,true);
}

rule: "Moira Action"
Event.OngoingPlayer
Player.Moira
{
    if(IsFiringPrimary())
    {
        moira_resource -= moira_heal_drain*tick_rate;
    }
    else
    {
        moira_resource += moira_heal_passive*tick_rate;
    }

    if(IsUsingAbility1())
    {
        SetJumpVerticalSpeed(EventPlayer(),50);
    }
    else if(IsUsingUltimate())
    {
        SetAbility1Enabled(EventPlayer(),false);
        self_move_speed += 7.2;
        SetMoveSpeed(EventPlayer(),self_move_speed);
        moira_self_heal = StartDamageOverTime(EventPlayer(),null,100,5);
        WaitUntil(!IsUsingUltimate(),100);
        StopDamageOverTime(moira_self_heal);
        self_move_speed -= 7.2;
        SetMoveSpeed(EventPlayer(),self_move_speed);
        SetAbility1Enabled(EventPlayer(),true);
        Heal(EventPlayer(),null,2);
    }
    else
    {
        SetJumpVerticalSpeed(EventPlayer(),100);
    }

    if(moira_resource >100)
    {
        moira_resource = 100;
    }

    SetAbilityResource(EventPlayer(),Button.PrimaryFire,moira_resource);
    Wait(tick_rate);
    LoopIf(HeroOf(EventPlayer()) == Hero.Moira);
}

rule: "Moira Damage"
Event.OnDamageDealt
Player.Moira
{
    if(EventAbility() == Button.SecondaryFire)
    {
        moira_resource += moira_heal_damage*tick_rate;
    }
    else if(EventAbility() == Button.Ability2)
    {
        Damage(Victim(),Attacker(),EventDamage()*0.3);
    }
    else if(EventAbility() == Button.Ultimate)
    {
        Damage(Victim(),Attacker(),EventDamage()*0.07);
    }
    
}

